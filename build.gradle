plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2023.4.2"
    // FRC3620 ****************************************************************
    id "com.gorylenko.gradle-git-properties" version "2.3.2"
    // ************************************************************************
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

// FRC3620 ********************************************************************
compileJava.options.encoding = 'UTF-8' 
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'
// ****************************************************************************

def ROBOT_MAIN_CLASS = "frc.robot.Main"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project DeployUtils.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                }
            }
        }
    }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for JNI.
wpi.java.debugJni = false

// Set this to true to enable desktop support.
def includeDesktopSupport = false

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 5.
dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    // implementation 'com.google.code.gson:gson:2.8.9'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.32' // JUL bridge

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.18.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.18.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.18.0'

    // https://mvnrepository.com/artifact/com.lmax/disruptor
    implementation group: 'com.lmax', name: 'disruptor', version: '3.4.4'

    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testImplementation 'junit:junit:4.13.1'
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.8.2") {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
}

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'

    // FRC3620 ********************************************************************
    // test wonderfulness
    filter {
        // https://stackoverflow.com/a/52879530/17887564
        setFailOnNoMatchingTests(false)

        includeTestsMatching 'DummyTest'
    }
    // ****************************************************************************
}

// Simulation configuration (e.g. environment variables).
wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    // FRC3620 ****************************************************************
    // this is so log4j2 can scan classes for plugins
    manifest {
        attributes(
            "Manifest-Version": "1.0",
            "Multi-Release": true
        )
    }
    // ************************************************************************
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Configure jar and deploy tasks
deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

// Configure string concat to always inline compile
tasks.withType(JavaCompile) {
    options.compilerArgs.add '-XDstringConcat=inline'
}

// FRC3620 ********************************************************************

// git wonderfulness
task printGitProperties {
    dependsOn generateGitProperties
    doLast {
        println "Project: " + project.ext.gitProps["project.dir"]
        println "Branch: " + project.ext.gitProps["git.branch"]
        println "Describe: " + project.ext.gitProps["git.commit.id.describe"]
        println "Dirty: "  + project.ext.gitProps["git.dirty"]
        println "Build time: " + project.ext.gitProps["build.time"]
        println "Build host: " + project.ext.gitProps["git.build.host"]
    }
}

def getBuildTime() {
    return new Date().toString();
}

gitProperties {
    // Customize directory using gitPropertiesResourceDir config
    // The directory in this config key is also added as a classpath entry
    // (so the git.properties file will be included in the final JAR file)
    customProperty "build.time", { getBuildTime() }
    customProperty "project.dir", { project.projectDir.getName() }
    customProperty 'git.commit.id.describe', { it.describe(tags: true, always: true) } 
    gitPropertiesResourceDir = file("${project.rootDir}/src/main/deploy")
    extProperty = 'gitProps'
}
generateGitProperties.finalizedBy printGitProperties // make sure we do things in the correct order
generateGitProperties.outputs.upToDateWhen { false } // make sure the generateGitProperties task always executes (even when git.properties is not changed)

tasks.processResources.dependsOn generateGitProperties // make sure we do things in the correct order
// https://stackoverflow.com/a/70648216/17887564
tasks.processResources.outputs.upToDateWhen { false }  // make sure the processResources task always runs

// ****************************************************************************

// this helps junit test results show up in output
// look at https://stackoverflow.com/a/36130467/17887564 for some more suggestions
tasks.withType(Test) { 
  testLogging {
    exceptionFormat "full"
    // events "passed", "failed" // , "started", "skipped"
    showStandardStreams true
    showExceptions true
  }
}
// ****************************************************************************
